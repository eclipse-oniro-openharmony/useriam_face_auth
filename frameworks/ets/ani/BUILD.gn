# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../face_auth.gni")

copy_taihe_idl("face_auth_copy_taihe_idl") {
  sources = [ "idl/ohos.userIAM.faceAuth.taihe" ]
}

taihe_generated_file_path = "$taihe_file_path/out/useriam/face_auth"
ohos_taihe("face_auth_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":face_auth_copy_taihe_idl" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.userIAM.faceAuth.ani.cpp",
    "$taihe_generated_file_path/src/ohos.userIAM.faceAuth.abi.c",
  ]
}

generate_static_abc("face_auth_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.userIAM.faceAuth.ets" ]
  is_boot_abc = "True"
  device_dst_file = "system/framework/ohos.userIAM.faceAuth.abc"
  dst_file = "$target_out_dir/ohos.userIAM.faceAuth.abc"
  out_puts = [ "$target_out_dir/ohos.userIAM.faceAuth.abc" ]
  dependencies = [ ":face_auth_taihe" ]
}

ohos_prebuilt_etc("face_auth_taihe_abc_etc") {
  source = "$target_out_dir/ohos.userIAM.faceAuth.abc"
  module_install_dir = "framework"
  deps = [ ":face_auth_taihe_abc" ]
  part_name = "face_auth"
  subsystem_name = "useriam"
}

taihe_shared_library("faceauth_ani") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
  remove_configs = [ "//build/config/compiler:no_exceptions" ]

  taihe_generated_file_path = "$taihe_generated_file_path"
  include_dirs = [
    "${face_auth_path}/common/logs",
    "${face_auth_path}/common/inc",
    "${face_auth_path}/common/utils",
  ]

  sources = get_target_outputs(":face_auth_taihe")

  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.userIAM.faceAuth.impl.cpp",
  ]

  deps = [
    ":face_auth_taihe",
    "${face_auth_path}/frameworks/ipc:faceauth_framework",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "runtime_core:ani",
    "runtime_core:libarkruntime",
  ]

  if (defined(global_parts_info.graphic_graphic_2d)) {
    external_deps += [ "graphic_surface:surface" ]
  }

  if (use_musl) {
    version_script = "face_auth_ani_map"
  }

  part_name = "face_auth"
  subsystem_name = "useriam"
}

group("face_auth_ani") {
  deps = [
    ":face_auth_taihe_abc_etc",
    ":faceauth_ani",
  ]
}
